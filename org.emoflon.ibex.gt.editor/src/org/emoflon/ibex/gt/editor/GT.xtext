grammar org.emoflon.ibex.gt.editor.GT with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate gT "http://www.emoflon.org/ibex/gt/editor/GT"

EditorGTFile:
	(imports+=EditorImport)*
	(rules+=Rule)*;

EditorImport:
	'import' name=STRING;

Rule:
	(abstract?='abstract')? 'rule' name=ID
	('('
	(parameters+=Parameter (',' parameters+=Parameter)*)?
	')')?
	('refines' superRules+=[Rule] (',' superRules+=[Rule])*)?
	('{'
	(nodes+=Node)*
	'}')?;

Parameter:
	name=ID ':' type=[ecore::EDataType];

	// Nodes
Node:
	(operator=OperatorForEditor)?
	name=ID ':' type=[ecore::EClass]
	('{'
	attributes+=AttributeConstraint*
	references+=EditorReference*
	'}')?;

enum Operator:
	CONTEXT |
	CREATE |
	DELETE;

enum OperatorForEditor returns Operator:
	CREATE='++' | DELETE='--';

	// Attribute Assignments and Conditions
AttributeConstraint:
	'.' attribute=[ecore::EAttribute] relation=Relation value=EditorExpression;

enum Relation:
	ASSIGNMENT=':=' |
	GREATER='>' |
	GREATER_OR_EQUAL='>=' |
	EQUAL='==' |
	UNEQUAL='!=' |
	SMALLER_OR_EQUAL='<=' |
	SMALLER='<';

EditorExpression:
	EditorAttributeExpression | EditorLiteralExpression | EditorEnumExpression | EditorParameterExpression;

EditorAttributeExpression:
	node=[Node] '.' attribute=[ecore::EAttribute];

EditorLiteralExpression:
	value=('true' | 'false' | Number) |
	{StringConstant} value=STRING;

Number:
	('-')? INT ('.' INT)?;

EditorEnumExpression:
	'enum::' literal=[ecore::EEnumLiteral];

EditorParameterExpression:
	'param::' parameter=[Parameter];

	// References
EditorReference:
	(operator=OperatorForEditor)? '-' type=[ecore::EReference] '->' target=[Node];
