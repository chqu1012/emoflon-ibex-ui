grammar org.emoflon.ibex.gt.editor.GT with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate gT "http://www.emoflon.org/ibex/gt/editor/GT"

EditorGTFile:
	(imports+=EditorImport)*
	(patterns+=EditorPattern |
	conditions+=EditorCondition)*;

EditorImport:
	'import' name=STRING;

EditorPattern:
	(abstract?='abstract')? type=EditorPatternType name=ID
	('('
	(parameters+=EditorParameter (',' parameters+=EditorParameter)*)?
	')')?
	('refines' superPatterns+=[EditorPattern] (',' superPatterns+=[EditorPattern])*)?
	('{'
	(nodes+=EditorNode)*
	'}')?
	('when' conditions+=[EditorCondition] ('&&' conditions+=[EditorCondition])*)?;

enum EditorPatternType:
	PATTERN='pattern' |
	RULE='rule';

EditorParameter:
	name=ID ':' type=[ecore::EDataType];

	// Nodes
EditorNode:
	(operator=OperatorInEditor)?
	name=ID ':' type=[ecore::EClass]
	('{'
	attributes+=EditorAttribute*
	references+=EditorReference*
	'}')?;

enum EditorOperator:
	CONTEXT |
	CREATE |
	DELETE;

enum OperatorInEditor returns EditorOperator:
	CREATE='++' |
	DELETE='--';

	// Attribute Assignments and Conditions
EditorAttribute:
	'.' attribute=[ecore::EAttribute] relation=EditorRelation value=EditorExpression;

enum EditorRelation:
	ASSIGNMENT=':=' |
	GREATER='>' |
	GREATER_OR_EQUAL='>=' |
	EQUAL='==' |
	UNEQUAL='!=' |
	SMALLER_OR_EQUAL='<=' |
	SMALLER='<';

EditorExpression:
	EditorAttributeExpression | EditorLiteralExpression | EditorEnumExpression | EditorParameterExpression;

EditorAttributeExpression:
	node=[EditorNode] '.' attribute=[ecore::EAttribute];

EditorLiteralExpression:
	value=('true' | 'false' | Number) |
	{StringConstant} value=STRING;

Number:
	('-')? INT ('.' INT)?;

EditorEnumExpression:
	'enum::' literal=[ecore::EEnumLiteral];

EditorParameterExpression:
	'param::' parameter=[EditorParameter];

	// References
EditorReference:
	(operator=OperatorInEditor)? '-' type=[ecore::EReference] '->' target=[EditorNode];

	// Graph Conditions
EditorCondition:
	'condition' name=ID
	'=' condition=EditorConditionExpression;

EditorConditionExpression:
	EditorOr;

EditorOr returns EditorConditionExpression:
	EditorAnd ({EditorOr.left=current} '||' right=EditorAnd)*;

EditorAnd returns EditorConditionExpression:
	EditorNegation ({EditorAnd.left=current} '&&' right=EditorNegation)*;

EditorNegation returns EditorConditionExpression:
	EditorBasicExpression |
	EditorNegativeExpression;

EditorBasicExpression returns EditorConditionExpression:
	EditorSimpleCondition |
	'(' EditorOr ')';

EditorNegativeExpression returns EditorConditionExpression:
	'!' (EditorSimpleCondition |
	'(' EditorOr ')');

EditorSimpleCondition returns EditorConditionExpression:
	{EditorConditionReference}
	'check' condition=[EditorCondition] |
	{EditorEnforce}
	'enforce' pattern=[EditorPattern] |
	{EditorForbid}
	'forbid' pattern=[EditorPattern] |
	{EditorConstraint}
	'if' premise=[EditorPattern] 'then' conclusions+=[EditorPattern] ('||' conclusions+=[EditorPattern])*;
